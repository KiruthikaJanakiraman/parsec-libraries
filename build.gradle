buildscript {
    repositories {
        mavenCentral()
    }

    dependencies {
        classpath 'com.bmuschko:gradle-nexus-plugin:2.3.1'
    }

}

plugins {
    id "maven-publish"
    id 'net.researchgate.release' version '2.5.0'
    id("io.github.gradle-nexus.publish-plugin") version "1.1.0"
    id 'signing'
}

release {
    failOnUnversionedFiles = false
    failOnCommitNeeded = false
    tagTemplate = '$name-$version'
    newVersionCommitMessage = '[Gradle Release Plugin][ci skip] new version release:'
}

wrapper {
    gradleVersion = '5.2.1'
}

allprojects {

    group = 'com.yahoo.parsec'

    apply plugin: 'java'
    apply plugin: 'maven-publish'
    apply plugin: 'maven'

    repositories {
        mavenCentral()
    }
}

//maven central settings
ext.'nexusUsername' = System.env.OSS_USER
ext.'nexusPassword' = System.env.OSS_PW
ext.'signing.keyId' = System.env.SIGNING_KEYID
ext.'signing.password' = System.env.SIGNING_PWD


//the encrypted secret key ring file is checked in and will be decrypted by travis
ext.'signing.secretKeyRingFile' = "${project.rootDir}/maintainer-secring.gpg"


subprojects {
    def pomConfig = {
        licenses {
            license {
                name "The Apache Software License, Version 2.0"
                url "http://www.apache.org/licenses/LICENSE-2.0.txt"
                distribution "repo"
            }
        }
        developers {
            developer {
                id "parsec-maintainers"
                name "parsec-maintainers"
                email "parsec-maintainers@verizonmedia.com"
            }
        }
        scm {
            connection 'scm:git:https://github.com/yahoo/parsec-libraries.git'
            developerConnection 'scm:git:https://github.com/yahoo/parsec-libraries.git'
            url 'https://github.com/yahoo/parsec-libraries/'
        }
    }
    task javadocJar(type: Jar) {
        classifier = 'javadoc'
        from javadoc
    }

    task sourcesJar(type: Jar) {
        classifier = 'sources'
        from sourceSets.main.allSource
    }




    //prepare for upload
    publishing {
        publications {
            mavenJava(MavenPublication) {
                from components.java
                artifact javadocJar
                artifact sourcesJar
                pom.withXml {
                    def root = asNode()
                    root.appendNode('description', 'The Parsec Java Libraries.')
                    root.appendNode('name', project.name)
                    root.appendNode('url', 'https://github.com/yahoo/parsec-libraries')
                    root.children().last() + pomConfig
                }
            }

        }

    }

    uploadArchives.dependsOn check
    afterReleaseBuild.dependsOn check
}


nexusPublishing {
    repositories {
        sonatype {
            nexusUrl = uri("https://oss.sonatype.org/service/local/")
            snapshotRepositoryUrl = uri("https://oss.sonatype.org/content/repositories/snapshots/")
            username = project.nexusUsername
            password = project.nexusPassword
        }
    }
}
